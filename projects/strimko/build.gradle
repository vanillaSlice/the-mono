plugins {
    id 'java'
    id 'idea'
    id 'org.unbroken-dome.test-sets' version '1.5.1'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

version = '1.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
    testCompile group: 'org.testfx', name: 'testfx-core', version: '4.0.15-alpha'
    testCompile group: 'org.testfx', name: 'testfx-junit5', version: '4.0.15-alpha'
    compile group: 'com.google.guava', name: 'guava', version: '27.0-jre'
}

testSets {
    e2eTest
}

test {
    useJUnitPlatform()
    dependsOn e2eTest
}

e2eTest {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '8.14'
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    executionData fileTree(project.rootDir.absolutePath).include('**/build/jacoco/*.exec')
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jacocoTestCoverageVerification {
    executionData fileTree(project.rootDir.absolutePath).include('**/build/jacoco/*.exec')
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

task buildFatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Strimko',
                'Implementation-Version': version,
                'Main-Class': 'lowe.mike.strimko.StrimkoApplication'
    }
    baseName = 'Strimko-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

build.finalizedBy buildFatJar
